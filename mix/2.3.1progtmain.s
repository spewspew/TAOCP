A	EQU	ENDP+2
MAX	EQU	A+100
HEAD	EQU	MAX+1

BUF	EQU	3000
	ORIG	BUF
	ALF	"VALUE"
	ALF	" IS  "

	ORIG	3500
MAIN	STZ	HEAD
	ENN4	ENDP-POOL
1H	ENT1	ENDP,4
	JMP	INSERT
	INC4	2
	J4NP	1B
	JMP	T1

VISIT	STJ	1F
	LDA	0,5(3:5)
	CHAR
	STA	BUF+2
	STX	BUF+3
	OUT	BUF(19)
1H	JMP	*

* Insertion into binary tree at HEAD
* ri1 points to the node to insert.
LINK	EQU	1:2
INSERT	STJ	2F
	LD3	HEAD		# P ← HEAD
	J3Z	EMPTY
	LDA	0,1(3:5)	# RA ← VAL(N)
1H	ENT2	0,3		# P ← Q
	CMPA	0,2(3:5)
	JL	LESS		# VAL(N) < VAL(P)
	JG	GREATER		# VAL(N) > VAL(P)
2H	JMP	*
LESS	LD3	0,2(LINK)	# Q ← LLINK(P)
	J3NZ	1B		# Jump if Q ≠ Λ
	ST1	0,2(LINK)	# LLINK(P) ← N
	STZ	0,1(LINK)	# LLINK(N) ← Λ
	STZ	1,1(LINK)	# RLINK(N) ← Λ
	JMP	2B		# Return
GREATER	LD3	1,2(LINK)
	J3NZ	1B
	ST1	1,2(LINK)
	STZ	0,1(LINK)
	STZ	1,1(LINK)
	JMP	2B
EMPTY	ST1	HEAD
	STZ	0,1(LINK)
	STZ	1,1(LINK)
	JMP	2B
